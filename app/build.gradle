apply plugin: 'com.android.application'
// Create by Eric Werner

// https://medium.com/@geocohn/keeping-your-android-projects-secrets-secret-393b8855765d
// https://stackoverflow.com/questions/46764175/secure-way-of-storing-secret-credentials
// --> This way your credentials will be baked in your app but only on build time
// Creates a variable called keystorePropertiesFile, and initializes it to the
// keystore.properties file.
def keystorePropertiesFile = rootProject.file("keystore.properties")
// Initializes a new Properties() object called keystoreProperties.
def keystoreProperties = new Properties()
// Loads the keystore.properties file into the keystoreProperties object.
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))


android {
    lintOptions {
        checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }
    signingConfigs {
        // DEBUG_RELEASE
        try {
            debug_release {
                storeFile file(keystoreProperties['DEBUG_RELEASE_KEY_PATH'])
                storePassword keystoreProperties['DEBUG_RELEASE_STORE_PASSWD']
                keyAlias keystoreProperties['DEBUG_RELEASE_KEY_ALIAS']
                keyPassword keystoreProperties['DEBUG_RELEASE_KEY_PASSWD']
            }
        } catch (Exception e) {
            logger.error("RELEASE")
            logger.error(e.getMessage())
        }
        // RELEASE
        try {
            release {
                storeFile file(keystoreProperties['RELEASE_KEY_PATH'])
                storePassword keystoreProperties['RELEASE_STORE_PASSWD']
                keyAlias keystoreProperties['RELEASE_KEY_ALIAS']
                keyPassword keystoreProperties['RELEASE_KEY_PASSWD']
            }
        } catch (Exception e) {
            logger.error("RELEASE")
            logger.error(e.getMessage())
        }
    }
    compileSdkVersion 28
    defaultConfig {
        applicationId "de.gruschtelapps.fh_maa_refuelpair"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        buildToolsVersion = '28.0.3'
        //
        // Builds
        //
        // https://developer.android.com/studio/build/build-variants
        debug {
            debuggable true
            minifyEnabled false
            versionNameSuffix '-DEBUG'
            applicationIdSuffix ".debug"
            // variable
            // https://stackoverflow.com/questions/42725299/whats-the-difference-between-buildconfigfield-resvalue-and-manifestplaceholder
            resValue "string", "app_name", "Fahrtenlog"
            buildConfigField "int", "DB_VERSION", "1"
            buildConfigField "int", "START_VIEW", "1"
            buildConfigField "String", "DB_PATH", "\"refuelpair_debug.db\""

            try {
                buildConfigField "String", "TANKERKOENIG_API", keystoreProperties['TANKERKOENIG_API']
            } catch (Exception e) {
                logger.error(e.getMessage())
                buildConfigField "String", "TANKERKOENIG_API", "null"
            }
        }

        debug_release {
            debuggable false
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            applicationIdSuffix ".debug_release"
            versionNameSuffix '-DEBUG_RELEASE'
            signingConfig signingConfigs.debug_release
            // variable
            resValue "string", "app_name", "DR_ReFuelPair"
            buildConfigField "int", "DB_VERSION", "1"
            buildConfigField "int", "START_VIEW", "1"
            buildConfigField "String", "DB_PATH", "\"refuelpair_debug.db\""

            try {
                buildConfigField "String", "TANKERKOENIG_API", keystoreProperties['TANKERKOENIG_API']
            } catch (Exception e) {
                logger.error(e.getMessage())
                buildConfigField "String", "TANKERKOENIG_API", "null"
            }
        }

        release {
            debuggable false
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            applicationIdSuffix ".release"
            versionNameSuffix '-RELEASE'
            // variable
            resValue "string", "app_name", "ReFuelPair"
            buildConfigField "int", "DB_VERSION", "1"
            buildConfigField "int", "START_VIEW", "1"
            buildConfigField "String", "DB_PATH", "\"refuelpair.db\""

            try {
                buildConfigField "String", "TANKERKOENIG_API", keystoreProperties['TANKERKOENIG_API']
            } catch (Exception e) {
                logger.error(e.getMessage())
                buildConfigField "String", "TANKERKOENIG_API", "null"
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support:support-v4:28.0.0'
    implementation 'com.android.support:design:28.0.0'
    // Don't update 'com.android.support.constraint:constraint-layout:2.0.0-beta1'
    // --> Destroys the layouts. Google has changed many parameters --> fix too complex because too many layouts
    implementation 'com.android.support.constraint:constraint-layout:2.0.0-beta1'
    implementation 'android.arch.lifecycle:extensions:1.1.1'
    testImplementation 'junit:junit:4.13-beta-2'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    implementation 'com.android.support:support-annotations:28.0.0'

    implementation 'com.github.clans:fab:1.6.4'
    implementation 'com.rengwuxian.materialedittext:library:2.1.4'
    implementation 'com.jakewharton.timber:timber:4.7.1'
    implementation 'com.squareup.okhttp3:okhttp:3.10.0'
    implementation 'com.github.PhilJay:MPAndroidChart:v3.1.0'
    implementation 'com.android.support:gridlayout-v7:28.0.0'
}
